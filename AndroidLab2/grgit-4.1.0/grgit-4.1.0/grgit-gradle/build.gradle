plugins {
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'

  id 'com.gradle.plugin-publish'
  id 'org.ajoberstar.stutter'
}

// avoid conflict with localGroovy()
configurations.all {
  exclude group: 'org.codehaus.groovy'
}

// compat tests use grgit to set up and verify the tests
sourceSets {
  compatTest {
    compileClasspath += main.output
    runtimeClasspath += main.output
  }
}

dependencies {
  compileOnly gradleApi()

  api project(':grgit-core')
  compatTestImplementation project(':grgit-core')

  compatTestCompile 'junit:junit:latest.release'
  compatTestCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'org.ajoberstar.grgit.gradle'
  }
}

stutter {
  sparse = true
  java(8) {
    compatibleRange '4.0'
  }
  java(11) {
    compatibleRange '5.0'
  }
  java(14) {
    compatibleRange '6.3'
  }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/grgit'
    vcsUrl = 'https://github.com/ajoberstar/grgit.git'
    description = 'The Groovy way to use Git'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.grgit'
            displayName = 'The Groovy way to use Git'
            tags = ['git', 'groovy']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

// remove duplicate publication
gradlePlugin.automatedPublishing = false
